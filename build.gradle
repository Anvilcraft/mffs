buildscript {
    repositories {
        mavenCentral()
        maven { url 'https://maven.tilera.xyz' }
        maven {
            name = "forge"
            url = "https://maven.minecraftforge.net/"
        }
        maven {
            name = "sonatype"
            url = "https://oss.sonatype.org/content/repositories/snapshots/"
        }
    }
    dependencies {
        classpath ('com.anatawa12.forge:ForgeGradle:1.2-1.0.+') {
            changing = true
        }
    }
}

apply plugin: 'forge'
apply plugin: 'maven-publish'

sourceCompatibility = JavaVersion.VERSION_1_8
targetCompatibility = JavaVersion.VERSION_1_8

sourceSets {
    api {}
}

version = "1.0.5"
group = "universalelectricity"
archivesBaseName = "mffs"

jar {
    manifest {
        attributes "FMLAT": "mffs_at.cfg"
    }
}

minecraft {
    version = "1.7.10-10.13.4.1614-1.7.10"
    runDir = "run"

    replaceIn "mffs/ModularForceFieldSystem.java"
    replace "{VERSION}", project.version
}

repositories {
    maven { url = "https://maven.tilera.xyz" }
    maven {                                                                                                                                                                               
            name = "ic2c"
            url = "https://maven.ic2.player.to/"
            metadataSources {
                artifact()
            }                                                                                                                                       
    }
}

dependencies {
    implementation "universalelectricity:basiccomponents:2.0.0:deobf"
    implementation "universalelectricity:universalelectricity:5.1.0:deobf"
    implementation 'net.industrial-craft:industrialcraft-2:2.2.827-experimental:dev'
}

processResources {
    // This will ensure that this task is redone when the versions change.
    inputs.property "version", project.version
    inputs.property "mcversion", project.minecraft.version

    // Replace values in only mcmod.info.
    filesMatching('mcmod.info') {
        // Replace version and mcversion.
        expand 'version':project.version, 'mcversion':project.minecraft.version
    }
}

task deobfJar(type: Jar) {
    from sourceSets.main.output
    classifier = 'deobf'
}

task sourcesJar(type: Jar) {
    from sourceSets.main.allSource
    classifier = 'sources'
}

publishing {
    tasks.publish.dependsOn 'build'
    publications {
        mavenJava(MavenPublication) {
            artifactId = project.archivesBaseName

            artifact deobfJar
            artifact sourcesJar
            artifact jar
        }
    }

    repositories {
	if (project.hasProperty('mvnURL')) {
		maven {
                    credentials {
                	username findProperty("mvnUsername")
                	password findProperty("mvnPassword")
            	    }
            	    url = findProperty("mvnURL")
        	}
	}
	else {
		mavenLocal()
	}
    }
}